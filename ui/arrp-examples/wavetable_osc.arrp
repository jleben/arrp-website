pi = 4*atan(1);

harmonic(k,N) = [N: i -> sin(k*i/N*2*pi)];

table_size = 500;
table =
    0.1 * harmonic(0, table_size) +
    0.5 * harmonic(1, table_size) +
    0.2 * harmonic(2, table_size) +
    0.3 * harmonic(3, table_size);

interpolate(table, i) = table[i0] * (1-d) + table[i1] * d where
{
    i0 =  int(i)    % #table;
    i1 = (int(i)+1) % #table;
    d  = i - floor(i);
};

phase(freq) = y where
{
    y[0] = 0.0;
    y[n] = wrap(y[n-1] + freq[n]);
    wrap(x) = if x < 1.0 then x else x - 1.0;
};

osc(table, freq) =
    [t -> interpolate(table, phase(freq)[t] * #table)];

sample_rate = 48000;
freq = 500;
main = 0.1 * osc(table, freq/sample_rate);

## To generate a Wave file with 1 second of the output at 48kHz:

## 1. Save this code into a file 'program.arrp', then compile it:
##     arrp program.arrp --exec program --exec-io raw

## 2. Run the program and pipe its output into 'ffmpeg' to write the audio file:
##     ./program | ffmpeg -y -t 1 -ac 1 -ar 48k -f f64le -i pipe:0 file:audio.wav
